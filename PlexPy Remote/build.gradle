plugins {
    id 'com.onesignal.androidsdk.onesignal-gradle-plugin' version '0.7.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.2"

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "com.williamcomartin.plexpyremote"
        minSdkVersion 17
        targetSdkVersion 27
        vectorDrawables.useSupportLibrary = true

        versionName "1.4"
        if (System.getenv("TRAVIS")) {
            versionCode System.getenv("TRAVIS_BUILD_NUMBER").toInteger()
        } else {
            versionCode 0
        }

        manifestPlaceholders = [onesignal_app_id               : "3b4b666a-d557-4b92-acdf-e2c8c4b95357",
                                onesignal_google_project_number: "159358117861"]
    }


    signingConfigs {
        release {
            storeFile rootProject.file('release.keystore')
            storePassword System.getenv("KEYSTORE_PASS")
            keyPassword System.getenv("ALIAS_PASS")
            keyAlias "PlexPyRemoteProdKey"
        }
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword "android"
            keyAlias "PlexPyRemote"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

if (System.getenv("TRAVIS")) {
    play {
        serviceAccountEmail = System.getenv("SERVICE_EMAIL")
        pk12File = rootProject.file('play.p12')
        track = 'alpha' // or 'alpha' or 'beta' or 'production'
        uploadImages = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android Support
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:percent:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:support-v4:27.0.2'

    implementation 'com.google.firebase:firebase-core:11.8.0'

    // HTTP Request
    implementation 'com.mcxiaoke.volley:library:1.0.19'
    implementation 'com.jakewharton:disklrucache:2.0.2'
    implementation 'com.google.code.gson:gson:2.8.0'

    implementation 'org.jdeferred:jdeferred-android-aar:1.2.4'

    // Icons
    implementation 'com.joanzapata.iconify:android-iconify:2.2.2'
    implementation 'com.joanzapata.iconify:android-iconify-fontawesome:2.2.2'
    implementation 'com.joanzapata.iconify:android-iconify-material:2.2.2'

    // QR Scanner
    implementation 'com.journeyapps:zxing-android-embedded:3.0.2@aar'
    implementation 'com.google.zxing:core:3.2.0'

    // 3rd Party UI
    implementation 'com.github.nirhart:parallaxscroll:1.0'
    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.11'

    implementation 'com.onesignal:OneSignal:3.6.5'
    // Required for OneSignal, even if you have added FCM.
    implementation 'com.google.android.gms:play-services-gcm:11.8.0'
    // Required for geotagging
    implementation "com.google.android.gms:play-services-location:11.8.0"

    implementation('com.mikepenz:aboutlibraries:5.9.6@aar') {
        transitive = true
    }

    implementation 'org.apache.commons:commons-lang3:3.6'

    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
